(define day-02
  (let* ((string-index (lambda (str chr start)
                         (let loop ((pos start))
                           (cond
                            ((<= (string-length str) pos) #f)
                            ((equal? chr (string-ref str pos)) pos)
                            (else (loop (+ 1 pos)))))))
         (string-split (lambda (str sep)
                         (let loop ((pos 0)
                                    (parts '()))
                           (let ((separator-pos (string-index str sep pos)))
                             (if separator-pos
                                 (loop (+ 1 separator-pos)
                                       (cons (substring str pos separator-pos)
                                             parts))
                                 (reverse (cons (substring str pos (string-length str)) parts)))))))
         (foldl (lambda (f init seq-in)
                  (let loop ((acc init)
                             (seq seq-in))
                    (if (null? seq)
                        acc
                        (loop (f (car seq) acc) (cdr seq))))))
         (giftbox-prop (lambda (f)
                         (lambda (giftbox-line)
                           (let* ((pieces (map string->number (string-split giftbox-line #\x)))
                                  (l (car pieces))
                                  (w (cadr pieces))
                                  (h (caddr pieces)))
                             (f l w h)))))
         (paper-amount (lambda (l w h)
                         (+ (min (* l w) (* l h) (* w h))
                            (* 2 l w) (* 2 l h) (* 2 w h))))
         (ribbon-amount (lambda (l w h)
                          (+ (* 2 (min (+ l w) (+ l h) (+ w h))) (* l w h))))
         (giftbox->area (giftbox-prop paper-amount))
         (giftbox->ribbon-amount (giftbox-prop ribbon-amount))
         (part-1 (lambda (lines)
                   (foldl + 0 (map giftbox->area lines))))
         (part-2 (lambda (lines)
                   (foldl + 0 (map giftbox->ribbon-amount lines)))))
    `((part-1 . ,part-1)
      (part-2 . ,part-2))))
